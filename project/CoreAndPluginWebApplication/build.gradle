buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
    }
}

ext {
    wrapperApp = ['webApp']
}


subprojects { Project project ->
    boolean isWrapper = wrapperApp.contains(project.name)

    apply plugin:"idea"
    if (isWrapper){
        apply plugin:"war"
        apply plugin:"org.grails.grails-web"
        apply plugin:"org.grails.grails-gsp"
    }else{
        apply plugin:"org.grails.grails-plugin"
        apply plugin:"org.grails.grails-plugin-publish"
        apply plugin:"org.grails.grails-gsp"
    }

    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://repo.grails.org/grails/core"
        }
        maven {
            url "http://central.maven.org/maven2/"
        }
    }

    dependencies {
        compile "org.springframework.boot:spring-boot-starter-logging"
        compile "org.springframework.boot:spring-boot-autoconfigure"
        compile "org.grails:grails-core"
        console "org.grails:grails-console"
    }

    if (isWrapper) {
        dependencies {
            compile "org.springframework.boot:spring-boot-starter-actuator"
            compile "org.springframework.boot:spring-boot-starter-tomcat"
            compile "org.grails:grails-dependencies"
            compile "org.grails:grails-web-boot"
            compile "org.grails.plugins:cache"
            compile "org.grails.plugins:scaffolding"
            compile "org.grails.plugins:hibernate5"
            compile "org.hibernate:hibernate-core:5.1.3.Final"
            compile "org.hibernate:hibernate-ehcache:5.1.3.Final"
            profile "org.grails.profiles:web"
            runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.1"
            runtime "com.h2database:h2"
            testCompile "org.grails:grails-plugin-testing"
            testCompile "org.grails.plugins:geb"
            testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
            testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
        }
    }else{
        dependencies {
            profile "org.grails.profiles:web-plugin"
            provided "org.grails:grails-plugin-services"
            provided "org.grails:grails-plugin-domain-class"
            testCompile "org.grails:grails-plugin-testing"
        }
    }

    bootRun {
        jvmArgs('-Dspring.output.ansi.enabled=always')
        addResources = true
    }

    if (!isWrapper) {
        bootRepackage.enabled = false
    }

}
